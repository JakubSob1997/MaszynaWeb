
// fibArg = 14;
// _1 = 1;

SOB start
fiboARG: RST 14
_0: RST 0
_1: RST 1
_3: RST 3



// while (fibArg >=0){
//  print(fiboRec(fibArg))
//  fibArg--;
// }

start: POB fiboARG
call: PSH
CALL fiboRec
OUT 4
POP
ODE _1
SOM end
SOB call
end:
STP

// fiboRec(int n){
//  if(n<=0)return 0;
//  if(n<3)return 1;
//  return fiboRec(n-1) + fiboRec(n-2);
// }

fiboRec:
TOPN 1 //AK = n
SOM default_0 //if(n<0)
SOZ default_0 //if(n==0)
ODE _3
SOM default_1 //if(n<3)
DOD _3


ODE _1// AK = n-1
PSH
CALL fiboRec //fiboRec(n-1)

POPAD tmp // tmp = n-1
PSH // psh val = fiboRec(n-1)
POB tmp
ODE _1
PSH //psh n-2

CALL fiboRec //fiboRec(n-2)

LAD tmp // val = fiboRec(n-2)
POP // n-2
POP // val = fiboRec(n-1)
DOD tmp // ak =fiboRec(n-1) + fiboRec(n-2)
RET 

default_0:
POB _0
RET
default_1:
POB _1
RET

tmp: RPA
























